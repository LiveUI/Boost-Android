import com.github.spotbugs.SpotBugsTask
import groovy.json.JsonSlurper

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.github.spotbugs'

apply plugin: 'kotlin-kapt'


android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "io.liveui.boost"
        minSdkVersion rootProject.ext.minSdkVersion
        versionName rootProject.ext.versionName
        versionCode getAppVersionCode()
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        resValue "string", "default_username", "admin@liveui.io"
        resValue "string", "default_password", "admin"
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
        debug {
            resValue "string", "default_username", "admin@liveui.io"
            resValue "string", "default_password", "admin"
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
//        loadFlavors().each { GenFlavor f ->
//            "$f.flavorName" {
//                java.srcDirs = ['src/main/java', 'src/production/java']
//                res.srcDirs = ["../flavors/$f.flavorName"]
//            }
//
//        }
    }

    flavorDimensions "default"

    productFlavors {

//        loadFlavors().each { GenFlavor f ->
//            "$f.flavorName" {
//                dimension "default"
//                buildConfigField "String", "BASE_URL", "\"$f.baseUrl\""
//            }
//        }

        production {
            dimension "default"
            buildConfigField "String[]", "URL", "{" +
                    "\"http://10.0.2.2:8080/\"," +
                    "\"http://boostappstore.herokuapp.com/\"" +
                    "}"
            resValue "string", "default_username", ""
            resValue "string", "default_password", ""
        }

        mock {
            dimension "default"
            buildConfigField "String[]", "URL", "{" +
                    "\"http://assets/\"" +
                    "}"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "AppStore-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }

}


tasks.withType(SpotBugsTask) {
    effort = "max"
    reportLevel = "low"
    excludeFilter = new File("../findbugs-android-exclude.xml")
    classes = files("$project.buildDir/intermediates/classes")
    source = fileTree('src/main/java/')
    classpath = files()
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.room:room-runtime:2.0.0'
    implementation 'androidx.room:room-rxjava2:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation "com.google.android:flexbox:${rootProject.ext.flexbox}"
    implementation "androidx.core:core-ktx:${rootProject.ext.ktx}"
    implementation "com.google.code.gson:gson:${rootProject.ext.gson}"
    implementation "com.google.dagger:dagger:${rootProject.ext.dagger}"
    implementation "com.google.dagger:dagger-android-support:${rootProject.ext.dagger}"
    implementation "com.google.guava:guava:${rootProject.ext.guava}"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:${rootProject.ext.rxbinding}"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:${rootProject.ext.rxbinding}"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:${rootProject.ext.rxbinding}"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:${rootProject.ext.rxbinding}"
    implementation "com.jakewharton.timber:timber:${rootProject.ext.timber}"
    implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${rootProject.ext.retrofit}"
    implementation "com.squareup.okhttp3:okhttp:${rootProject.ext.okHttp}"
    implementation "com.squareup.okio:okio:${rootProject.ext.okio}"
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.okHttpLogging}"
    implementation "com.github.bumptech.glide:annotations:${rootProject.ext.glide}"
    implementation "com.github.bumptech.glide:glide:${rootProject.ext.glide}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${rootProject.ext.glide}"
    implementation "io.reactivex.rxjava2:rxjava:${rootProject.ext.rxjava}"
    implementation "io.reactivex.rxjava2:rxandroid:${rootProject.ext.rxandroid}"
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.2'

    kapt "com.github.bumptech.glide:compiler:${rootProject.ext.glide}"
    kapt "com.google.dagger:dagger-compiler:${rootProject.ext.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${rootProject.ext.dagger}"
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    kapt 'androidx.room:room-compiler:2.0.0'

    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'androidx.room:room-testing:2.0.0'
    testImplementation "org.mockito:mockito-inline:2.15.0"
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0-alpha4'
    androidTestImplementation "org.hamcrest:hamcrest-integration:${rootProject.ext.hamcrest}"
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4') {
        exclude module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    }
    androidTestImplementation('androidx.test:runner:1.1.0-alpha4') {
        exclude module: 'support-annotations'
    }

    androidTestImplementation('androidx.test:rules:1.1.0-alpha4') {
        exclude module: 'support-annotations'
    }
}


static def getAppVersionCode() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHH')
    return formattedDate.toInteger()
}


class GenFlavor {
    String applicationId
    String applicationName
    String primaryColorHeader
    String primaryColorButton
    String baseUrl
    String flavorName

    @Override
    String toString() {
        return "GenFlavor{" +
                "applicationId='" + applicationId + '\'' +
                ", applicationName='" + applicationName + '\'' +
                ", primaryColorHeader='" + primaryColorHeader + '\'' +
                ", primaryColorButton='" + primaryColorButton + '\'' +
                '}'
    }
}


ArrayList<GenFlavor> loadFlavors() {
    println("Start generating flavors")
    File treeFolder = new File("flavors")
    if (treeFolder.listFiles() == null || treeFolder.listFiles().size() == 0) return ""

    ArrayList<GenFlavor> flavors = new ArrayList<>()

    for (File file : treeFolder.listFiles()) {
        if (!file.directory) continue

        def inputFile = new File("flavors/$file.name/config.json")
        def json = new JsonSlurper().parseText(inputFile.text)

        GenFlavor f = new GenFlavor()
        f.flavorName = file.name
        f.applicationId = json.applicationId
        f.applicationName = json.applicationName
        f.primaryColorButton = json.primaryColorButton
        f.primaryColorHeader = json.primaryColorHeader
        f.baseUrl = json.baseUrl
        flavors.add(f)
    }

    return flavors
}
